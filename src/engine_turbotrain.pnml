/* 
 * =============================================================================
 * Turner Turbotrain
 * =============================================================================
 */

/*
 * Turbo train engine (arctic)
 * Livery override for passenger and mail wagons
 */
spriteset(turbotrain_engine_set, "src/gfx/turbotrain.png") {
	[142,112,  8,22,   -3,-10]
	[158,112, 21,15,  -14, -7]
	[190,112, 31,12,  -16, -8]
	[238,112, 21,16,   -6, -7]
	[270,112,  8,24,   -3,-10]
	[286,112, 21,16,  -15, -6]
	[318,112, 32,12,  -16, -8]
	[366,112, 21,15,   -6, -7]
}
spritegroup turbotrain_engine_group {
	loading: turbotrain_engine_set;
	loaded: turbotrain_engine_set;
}

CAN_ATTACH_VEHICLE(turbotrain, 20)

switch(FEAT_TRAINS, SELF, turbotrain_engine_switch, current_callback) {
    VEH_CB_CAN_ATTACH_WAGON: turbotrain_can_attach_switch;
    turbotrain_engine_group;
}

if (PROVIDE_ENGINES) {
	// Turbotrain engine:
	item(FEAT_TRAINS, turbotrain, 20) {
		property {
			sprite_id:                    SPRITE_ID_NEW_TRAIN;    // We have our own sprites
			misc_flags:                   bitmask(TRAIN_FLAG_MU); // We use special sprites for passenger and mail wagons
		}
		graphics {
			turbotrain_engine_switch;
		}
		livery_override(passenger_wagon) { 
			turbotrain_passenger_group;
		}
		livery_override(mail_wagon) {
			turbotrain_mail_group;
		} 
	}
}
