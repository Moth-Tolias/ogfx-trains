/* 
 * ***************************
 * Flatbed wagon:
 *
 * wood, lumber, metal, steel, glass,
 * goods, manufacturing supplies,
 * farm supplies, engineering supplies,
 * batteries, fizzy drinks, bubbles
 * ***************************
 */

spriteset(monorail_flatbed_paper_empty_set, "src/gfx/arctic_monolev.png") { tmpl_short_railwagon(0,320) }
spriteset(monorail_flatbed_paper_full_set, "src/gfx/arctic_monolev.png") { tmpl_short_railwagon(0,340) }
spritegroup monorail_flatbed_paper_group {
	loaded: [monorail_flatbed_paper_empty_set, monorail_flatbed_paper_full_set];
	loading: [monorail_flatbed_paper_empty_set, monorail_flatbed_paper_full_set];
}

spriteset(monorail_flatbed_wood_empty_set, "src/gfx/temperate_monolev.png") { tmpl_short_railwagon(0,280) }
spriteset(monorail_flatbed_wood_full_set, "src/gfx/temperate_monolev.png") { tmpl_short_railwagon(0,300) }
spritegroup monorail_flatbed_wood_group {
	loaded: [monorail_flatbed_wood_empty_set, monorail_flatbed_wood_full_set];
	loading: [monorail_flatbed_wood_empty_set, monorail_flatbed_wood_full_set];
}
spriteset(monorail_flatbed_tropicwood_empty_set, "src/gfx/tropic_monolev.png") { tmpl_short_railwagon(0,200) }
spriteset(monorail_flatbed_tropicwood_full_set, "src/gfx/tropic_monolev.png") { tmpl_short_railwagon(0,220) }
spritegroup monorail_flatbed_tropicwood_group {
	loaded: [monorail_flatbed_tropicwood_empty_set, monorail_flatbed_tropicwood_full_set];
	loading: [monorail_flatbed_tropicwood_empty_set, monorail_flatbed_tropicwood_full_set];
}

spriteset(monorail_flatbed_steel_empty_set, "src/gfx/temperate_monolev.png") { tmpl_short_railwagon(0,320) }
spriteset(monorail_flatbed_steel_full_set, "src/gfx/temperate_monolev.png") { tmpl_short_railwagon(0,340) }
spritegroup monorail_flatbed_steel_group {
	loaded: [monorail_flatbed_steel_empty_set, monorail_flatbed_steel_full_set];
	loading: [monorail_flatbed_steel_empty_set, monorail_flatbed_steel_full_set];
}

switch(FEAT_TRAINS, SELF, monorail_flatbed_wood_climate_switch, climate) {
	CLIMATE_TROPIC: monorail_flatbed_tropicwood_group;
	monorail_flatbed_wood_group;
}

switch(FEAT_TRAINS, SELF, monorail_flatbed_graphics_switch, cargo_type_in_veh) {
	PAPR: monorail_flatbed_paper_group;
	STEL: monorail_flatbed_steel_group;
	WOOD: monorail_flatbed_wood_climate_switch;
	flatbed_wagon_switch;
}

switch (FEAT_TRAINS, SELF, monorail_flatbed_switch, current_callback) {
    VEH_CB_REFITTED_CAPACITY:  flatbed_wagon_cb_capacity_switch; // use rail wagon properties
    VEH_CB_VEHICLE_PROPERTIES: flatbed_wagon_cb_property_switch; // use rail wagon properties
    monorail_flatbed_graphics_switch;
}

item(FEAT_TRAINS, monorail_flatbed_wagon) {
    property {
        name:                           string(STR_NAME_FLATBED_WAGON);
        climates_available:             ALL_CLIMATES;
        refittable_cargo_classes:       bitmask(CC_PIECE_GOODS);
        non_refittable_cargo_classes:   bitmask(CC_PASSENGERS, CC_MAIL, CC_ARMOURED, CC_BULK, CC_LIQUID, CC_REFRIGERATED, CC_HAZARDOUS, CC_COVERED);
        refittable_cargo_types:         bitmask(LVST, WOOL, SCRP, TOYS, WDPR);
        sprite_id:                      SPRITE_ID_NEW_TRAIN;
        introduction_date:              date(1980,1,1);
        model_life:                     50;
        retire_early:                   0;
        vehicle_life:                   20;
        reliability_decay:              20;
        loading_speed:                  5;
        cost_factor:                    181;
        running_cost_factor:            5;
        speed:                          0;
        refit_cost:                     40;
        callback_flags:                 bitmask(VEH_CBF_REFITTED_CAPACITY);
        track_type:                     MONO;
        power:                          0;
        running_cost_base:              RUNNING_COST_STEAM;
        cargo_capacity:                 30; // Usually units of goods which fit onto it.
        weight:                         16;
        bitmask_vehicle_info:           0;
    }
    graphics {
        monorail_flatbed_switch;
    }
}