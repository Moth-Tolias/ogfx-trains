/*
 * ***************************
 * Flatbed wagon:
 *
 * wood, lumber, metal, steel, glass,
 * goods, manufacturing supplies,
 * farm supplies, engineering supplies,
 * batteries, fizzy drinks, bubbles
 * ***************************
 */

spriteset(monorail_flatbed_paper_empty_set, "src/gfx/arctic_monolev.png") { tmpl_short_railwagon(0,320) }
spriteset(monorail_flatbed_paper_full_set, "src/gfx/arctic_monolev.png") { tmpl_short_railwagon(0,340) }
spritegroup monorail_flatbed_paper_group {
	loaded: [monorail_flatbed_paper_empty_set, monorail_flatbed_paper_full_set];
	loading: [monorail_flatbed_paper_empty_set, monorail_flatbed_paper_full_set];
}

spriteset(monorail_flatbed_wood_empty_set, "src/gfx/temperate_monolev.png") { tmpl_short_railwagon(0,280) }
spriteset(monorail_flatbed_wood_full_set, "src/gfx/temperate_monolev.png") { tmpl_short_railwagon(0,300) }
spritegroup monorail_flatbed_wood_group {
	loaded: [monorail_flatbed_wood_empty_set, monorail_flatbed_wood_full_set];
	loading: [monorail_flatbed_wood_empty_set, monorail_flatbed_wood_full_set];
}
spriteset(monorail_flatbed_tropicwood_empty_set, "src/gfx/tropic_monolev.png") { tmpl_short_railwagon(0,200) }
spriteset(monorail_flatbed_tropicwood_full_set, "src/gfx/tropic_monolev.png") { tmpl_short_railwagon(0,220) }
spritegroup monorail_flatbed_tropicwood_group {
	loaded: [monorail_flatbed_tropicwood_empty_set, monorail_flatbed_tropicwood_full_set];
	loading: [monorail_flatbed_tropicwood_empty_set, monorail_flatbed_tropicwood_full_set];
}

spriteset(monorail_flatbed_steel_empty_set, "src/gfx/temperate_monolev.png") { tmpl_short_railwagon(0,320) }
spriteset(monorail_flatbed_steel_full_set, "src/gfx/temperate_monolev.png") { tmpl_short_railwagon(0,340) }
spritegroup monorail_flatbed_steel_group {
	loaded: [monorail_flatbed_steel_empty_set, monorail_flatbed_steel_full_set];
	loading: [monorail_flatbed_steel_empty_set, monorail_flatbed_steel_full_set];
}

switch(FEAT_TRAINS, SELF, monorail_flatbed_wood_climate_switch, climate) {
	CLIMATE_TROPIC: monorail_flatbed_tropicwood_group;
	monorail_flatbed_wood_group;
}

switch(FEAT_TRAINS, SELF, monorail_flatbed_graphics_switch, cargo_type_in_veh) {
	PAPR: monorail_flatbed_paper_group;
	STEL: monorail_flatbed_steel_group;
	COPR: monorail_flatbed_steel_group;
	WOOD: monorail_flatbed_wood_climate_switch;
	flatbed_wagon_switch;
}

item(FEAT_TRAINS, monorail_flatbed_wagon) {
    property {
        name:                           string(STR_NAME_FLATBED_WAGON);
        REFIT_PROPERTIES_FLATBED
        WAGON_PROPERTIES_DEFAULT
        WAGON_PROPERTIES_MONORAIL_DEFAULT
        cost_factor:                    181;
        callback_flags:                 bitmask(VEH_CBF_REFITTED_CAPACITY);
        cargo_capacity:                 30; // Usually units of goods which fit onto it.
        weight:                         16;
    }
    graphics {
        cargo_capacity:     flatbed_wagon_cb_capacity_switch;        // same as rail wagon
        weight:             flatbed_wagon_cb_property_weight_switch; // same as rail wagon
        cargo_subtype_text: flatbed_wagon_cb_cargo_suffix_switch;    // same as rail wagon
        colour_mapping:     flatbed_wagon_cb_colour_switch;          // same as rail wagon
        random_trigger:     flatbed_wagon_cb_colour_switch;          // same as rail wagon
        refit_cost:         flatbed_wagon_refit_cost_switch;

        purchase:           monorail_flatbed_wood_climate_switch;
        monorail_flatbed_graphics_switch;
    }
}
