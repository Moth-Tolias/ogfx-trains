/* 
 * ***************************
 * Tank wagon:
 *
 * oil, fuel oil, water, milk,
 * rubber, dyes, chemicals,
 * cola
 * ***************************
 */

spriteset(tank_wagon_default_temperate_set, "src/gfx/temperate_railwagons.png") { tmpl_railwagon(0,75) }
spriteset(tank_wagon_default_tropic_set, "src/gfx/tropic_railwagons.png") { tmpl_railwagon(0,75) }
spritegroup tank_wagon_default_temperate_group {
	loaded: tank_wagon_default_temperate_set;
	loading: tank_wagon_default_temperate_set;
}
spritegroup tank_wagon_default_tropic_group {
	loaded: tank_wagon_default_tropic_set;
	loading: tank_wagon_default_tropic_set;
}

spriteset(tank_wagon_chemicals_set, "src/gfx/railwagon_chemicals.png") { tmpl_railwagon(0,0) }
spritegroup tank_wagon_chemicals_group {
	loaded: tank_wagon_chemicals_set;
	loading: tank_wagon_chemicals_set;
}

spriteset(tank_wagon_water_set, "src/gfx/tropic_railwagons.png") { tmpl_railwagon(0,475) }
spritegroup tank_wagon_water_group {
	loaded: tank_wagon_water_set;
	loading: tank_wagon_water_set;
}

spriteset(tank_wagon_cola_empty_set, "src/gfx/toyland_railwagons.png") { tmpl_short_railwagon(0,220) }
spriteset(tank_wagon_cola_full_set, "src/gfx/toyland_railwagons.png") { tmpl_short_railwagon(0,240) }
spritegroup tank_wagon_cola_group {
	loaded: [tank_wagon_cola_empty_set, tank_wagon_cola_full_set];
	loading: [tank_wagon_cola_empty_set, tank_wagon_cola_full_set];
}

switch(FEAT_TRAINS, SELF, tank_wagon_default_switch, climate) {
    CLIMATE_TROPICAL: tank_wagon_default_tropic_group;
    tank_wagon_default_temperate_group;
}

switch(FEAT_TRAINS, SELF, tank_wagon_switch, cargo_type_in_veh) {
    COLA: tank_wagon_cola_group;
    MILK: tank_wagon_default_tropic_group;
    PETR: tank_wagon_chemicals_group;
    RFPR: tank_wagon_chemicals_group;
    WATR: tank_wagon_water_group;
    tank_wagon_default_switch;
}

switch (FEAT_TRAINS, SELF, tank_wagon_cb_capacity_switch, cargo_type_in_veh) {
    // default 30.000l
    COLA: return 25;
    FZDR: return 25;
    MILK: return 25;
    WATR: return 25;
    CB_FAILED;
}

switch (FEAT_TRAINS, SELF, tank_wagon_cb_property_weight_switch, cargo_type_in_veh) {
    // default: 24 tons
    WATR: return 25;
    MILK: return 25;
    CB_FAILED;
}

switch (FEAT_TRAINS, SELF, tank_wagon_cb_property_switch, extra_callback_info1) {
    0x16: tank_wagon_cb_property_weight_switch;
    0x14: tank_wagon_cb_capacity_switch;         // also needed as CB_REFITTED_CAPACITY doesn't work for default cargo
    CB_FAILED;
}

switch (FEAT_TRAINS, SELF, tank_wagon_cb_switch, current_callback) {
    VEH_CB_REFITTED_CAPACITY:  tank_wagon_cb_capacity_switch;
    VEH_CB_VEHICLE_PROPERTIES: tank_wagon_cb_property_switch;
    tank_wagon_switch;
}


item(FEAT_TRAINS, tank_wagon) {
    property {
        // We try to simulate the stats of the temperate oil wagon
        name:                           string(STR_NAME_TANK_WAGON);
        climates_available:             ALL_CLIMATES;
        refittable_cargo_classes:       bitmask(CC_LIQUID);
        non_refittable_cargo_classes:   bitmask(CC_PASSENGERS, CC_MAIL, CC_ARMOURED, CC_PIECE_GOODS);
        refittable_cargo_types:         bitmask(CMNT, FZDR);
        sprite_id:                      SPRITE_ID_NEW_TRAIN;
        introduction_date:              date(1925,1,1);
        model_life:                     50;
        retire_early:                   0;
        vehicle_life:                   20;
        reliability_decay:              20;
        loading_speed:                  5;
        cost_factor:                    200;
        running_cost_factor:            5;
        speed:                          0;
        refit_cost:                     40;
        callback_flags:                 bitmask(VEH_CBF_REFITTED_CAPACITY);
        track_type:                     RAIL;
        power:                          0;
        running_cost_base:              RUNNING_COST_STEAM;
        cargo_capacity:                 30;
        weight:                         24;
        bitmask_vehicle_info:           0;
    }
    graphics {
        tank_wagon_cb_switch;
    }
}