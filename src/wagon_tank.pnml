/*
 * ***************************
 * Tank wagon:
 *
 * oil, fuel oil, water, milk,
 * rubber, dyes, chemicals,
 * cola
 * ***************************
 */
// CMNT:
spriteset(tank_wagon_cement_set, "src/gfx/tank/rail_cement_temperate.png") { tmpl_railwagon(0, 0) }
spritegroup tank_wagon_cement_group {
	loaded: tank_wagon_cement_set;
	loading: tank_wagon_cement_set;
}

// COLA
spriteset(tank_wagon_cola_empty_set, "src/gfx/toyland_railwagons.png") { tmpl_short_railwagon(0,220) }
spriteset(tank_wagon_cola_full_set, "src/gfx/toyland_railwagons.png") { tmpl_short_railwagon(0,240) }
spritegroup tank_wagon_cola_group {
	loaded: [tank_wagon_cola_empty_set, tank_wagon_cola_full_set];
	loading: [tank_wagon_cola_empty_set, tank_wagon_cola_full_set];
}

// MILK:
spriteset(tank_wagon_milk_set, "src/gfx/tank/rail_milk_temperate.png") { tmpl_railwagon(0, 0) }
spritegroup tank_wagon_milk_group {
	loaded: tank_wagon_milk_set;
	loading: tank_wagon_milk_set;
}

// OIL_
spriteset(tank_wagon_default_temperate_set, "src/gfx/tank/rail_oil_temperate.png") { tmpl_railwagon(0, 0) tmpl_railwagon(0, 0) }
spriteset(tank_wagon_default_tropic_set, "src/gfx/tropic_railwagons.png") { tmpl_railwagon(0,75) }

/* OpenGFX+ */
/* Define the 32bpp sprite set for normal zoom */
alternative_sprites(tank_wagon_default_temperate_set, ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP) {
    template_train_opengfxp32(1, "tank", "1")
}

/* OpenGFX+ */
/* Define the 32bpp sprite set for 2X zoom in */
alternative_sprites(tank_wagon_default_temperate_set, ZOOM_LEVEL_IN_2X, BIT_DEPTH_32BPP) {
    template_train_opengfxp32(2, "tank", "2")
}

/* OpenGFX+ */
/* Define the 32bpp sprite set for 4X zoom in */
alternative_sprites(tank_wagon_default_temperate_set, ZOOM_LEVEL_IN_4X, BIT_DEPTH_32BPP) {
    template_train_opengfxp32(4, "tank", "4")
}

/* OpenGFX+ */
/* Define the 8bpp sprite set for the GUI menus */
spriteset(tank_wagon_default_temperate_set_gui, "src/gfx/tank/rail_oil_temperate.png") {
	tmpl_railwagon(0, 0) tmpl_railwagon(0, 0)               // 8 views needed for livery override by turbotrain
}

/* OpenGFX+ */
/* Define the 32bpp sprite set for the GUI menus */
alternative_sprites(tank_wagon_default_temperate_set_gui, ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP) {
    template_train_gui_opengfxp32("tank")
}

/* OpenGFX+ */
/* Define the sprite group for the GUI menus */
spritegroup tank_wagon_default_temperate_group_gui {
	loading: tank_wagon_default_temperate_set_gui;
	loaded: tank_wagon_default_temperate_set_gui;
}

spritegroup tank_wagon_default_temperate_group {
	loaded: tank_wagon_default_temperate_set;
	loading: tank_wagon_default_temperate_set;
}
spritegroup tank_wagon_default_tropic_group {
	loaded: tank_wagon_default_tropic_set;
	loading: tank_wagon_default_tropic_set;
}

// PETR
spriteset(tank_wagon_fueloil_set, "src/gfx/tank/rail_fueloil_temperate.png") { tmpl_railwagon(0, 0) }
spritegroup tank_wagon_fueloil_group {
	loaded: tank_wagon_fueloil_set;
	loading: tank_wagon_fueloil_set;
}

// RFPR:
spriteset(tank_wagon_refineryproducts_set, "src/gfx/tank/rail_refineryproducts_temperate.png") { tmpl_railwagon(0, 4) }
spritegroup tank_wagon_refineryproducts_group {
	loaded: tank_wagon_refineryproducts_set;
	loading: tank_wagon_refineryproducts_set;
}

// WATR
spriteset(tank_wagon_water_set, "src/gfx/tank/rail_water_temperate.png") { tmpl_railwagon(0, 0) }
spritegroup tank_wagon_water_group {
	loaded: tank_wagon_water_set;
	loading: tank_wagon_water_set;
}


/* OpenGFX+ */
/* Define the switch for the GUI menus */
switch(FEAT_TRAINS, SELF, tank_wagon_default_switch_gui, (extra_callback_info1 & 0xFF)) {
    0x10: tank_wagon_default_temperate_group_gui;
    0x11: tank_wagon_default_temperate_group_gui;
    0x12: tank_wagon_default_temperate_group_gui;
    0x20: tank_wagon_default_temperate_group_gui;
    0x21: tank_wagon_default_temperate_group_gui;
    tank_wagon_default_temperate_group;
}

switch(FEAT_TRAINS, SELF, tank_wagon_default_switch, climate) {
    CLIMATE_TROPICAL: tank_wagon_default_tropic_group;
    tank_wagon_default_switch_gui;
}

switch(FEAT_TRAINS, SELF, tank_wagon_switch, cargo_type_in_veh) {
	CMNT: tank_wagon_cement_group;
    COLA: tank_wagon_cola_group;
    FZDR: tank_wagon_default_switch;
    MILK: tank_wagon_milk_group;
    OIL_: tank_wagon_default_switch;
    PETR: tank_wagon_fueloil_group;
    RFPR: tank_wagon_refineryproducts_group;
    WATR: tank_wagon_water_group;
    tank_wagon_default_switch;
}

switch (FEAT_TRAINS, SELF, tank_wagon_cb_capacity_switch, cargo_type_in_veh) {
    // default 30.000l
    COLA: return 25;
	CMNT: return 15;
    FZDR: return 25;
    MILK: return 25;
    WATR: return 25;
    CB_FAILED;
}

switch (FEAT_TRAINS, SELF, tank_wagon_cb_property_weight_switch, cargo_type_in_veh) {
    // default: 24 tons
    WATR: return 25;
    MILK: return 25;
    CB_FAILED;
}

switch (FEAT_TRAINS, SELF, tank_wagon_refit_from_clean_switch, extra_callback_info1 & 0xFF) {
	COLA: return 20 | CB_RESULT_AUTOREFIT;
	FZDR: return 20 | CB_RESULT_AUTOREFIT;
	MILK: return 20 | CB_RESULT_AUTOREFIT;
	WATR: return 20 | CB_RESULT_AUTOREFIT;
	return 10 | CB_RESULT_AUTOREFIT;
}
switch (FEAT_TRAINS, SELF, tank_wagon_refit_from_dirty_switch, extra_callback_info1 & 0xFF) {
	COLA: CB_FAILED;
	FZDR: CB_FAILED;
	MILK: CB_FAILED;
	WATR: CB_FAILED;
	return 40 | CB_RESULT_AUTOREFIT;
}
switch (FEAT_TRAINS, SELF, tank_wagon_refit_cost_switch, cargo_type_in_veh) {
	CMNT: tank_wagon_refit_from_dirty_switch;
    COLA: tank_wagon_refit_from_clean_switch;
    FZDR: tank_wagon_refit_from_clean_switch;
    MILK: tank_wagon_refit_from_clean_switch;
    OIL_: tank_wagon_refit_from_dirty_switch;
    PETR: tank_wagon_refit_from_dirty_switch;
    RFPR: tank_wagon_refit_from_dirty_switch;
    WATR: tank_wagon_refit_from_clean_switch;
    return CB_FAILED;
}


item(FEAT_TRAINS, tank_wagon) {
    property {
        // We try to simulate the stats of the temperate oil wagon
        name:                           string(STR_NAME_TANK_WAGON);
        REFIT_PROPERTIES_TANK
        WAGON_PROPERTIES_DEFAULT
        WAGON_PROPERTIES_RAIL_DEFAULT
        cost_factor:                    200;
        cargo_capacity:                 30;
        weight:                         24;
        misc_flags:                     bitmask(TRAIN_FLAG_2CC);
    }
    graphics {
		cargo_capacity:     tank_wagon_cb_capacity_switch;
		weight:             tank_wagon_cb_property_weight_switch;
        refit_cost:         tank_wagon_refit_cost_switch;

        purchase: tank_wagon_default_switch;
        tank_wagon_default_switch;
    }
}
