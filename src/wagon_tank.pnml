/*
 * This file is part of OpenGFX+ Trains.
 * OpenGFX+ Trains is free software; you can redistribute it and/or modify it under the terms of the GNU General Public License as published by the Free Software Foundation, version 2.
 * OpenGFX+ Trains is distributed in the hope that it will be useful, but WITHOUT ANY WARRANTY; without even the implied warranty of MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.
 * See the GNU General Public License for more details. You should have received a copy of the GNU General Public License along with OpenGFX+ Trains. If not, see <http://www.gnu.org/licenses/>.
 */

/*
 * ***************************
 * Tank wagon:
 *
 * oil, fuel oil, water, milk,
 * rubber, dyes, chemicals,
 * cola
 * ***************************
 */

// DEFAULT
MU_8SPRITES_32BPP(tank_wagon_default_temperate, "tank", "src/gfx/tank/rail_oil_temperate.png", tmpl_railwagon42, 0, 0) // 8 views needed
MU_8SPRITES_32BPP(tank_wagon_default_tropic, "tank", "src/gfx/tropic_railwagons.png", tmpl_railwagon42, 0, 75) // 8 views needed

// CMNT:
MU_8SPRITES_32BPP(tank_wagon_cement, "tank", "src/gfx/tank/rail_cement_temperate.png", tmpl_railwagon42, 0, 0) // 8 views needed

// COLA
spriteset(tank_wagon_cola_empty_set, "src/gfx/toyland_railwagons.png") { tmpl_short_railwagon(0,220) }
spriteset(tank_wagon_cola_full_set, "src/gfx/toyland_railwagons.png") { tmpl_short_railwagon(0,240) }
spritegroup tank_wagon_cola_group {
	loaded: [tank_wagon_cola_empty_set, tank_wagon_cola_full_set];
	loading: [tank_wagon_cola_empty_set, tank_wagon_cola_full_set];
}

// MILK:
MU_8SPRITES_32BPP(tank_wagon_milk, "tank_milk", "src/gfx/tank/rail_milk_temperate.png", tmpl_railwagon42, 0, 0) // 8 views needed

// OIL_
MU_8SPRITES_32BPP(tank_wagon_oil, "tank_oil", "src/gfx/tank/rail_oil_temperate.png", tmpl_railwagon42, 0, 0) // 8 views needed

// PETR
MU_8SPRITES_32BPP(tank_wagon_fueloil, "tank_petrol", "src/gfx/tank/rail_fueloil_temperate.png", tmpl_railwagon42, 0, 0) // 8 views needed

// RFPR:
MU_8SPRITES_32BPP(tank_wagon_refineryproducts, "tank_refinery", "src/gfx/tank/rail_refineryproducts_temperate.png", tmpl_railwagon42, 0, 4) // 8 views needed

// WATR
MU_8SPRITES_32BPP(tank_wagon_water, "tank_water", "src/gfx/tank/rail_water_temperate.png", tmpl_railwagon42, 0, 0) // 8 views needed

switch(FEAT_TRAINS, SELF, tank_wagon_default_climate_switch, climate) {
    CLIMATE_TROPICAL: tank_wagon_default_tropic_switch_gui;
    tank_wagon_default_temperate_switch_gui;
}

switch(FEAT_TRAINS, SELF, tank_wagon_oil_climate_switch, climate) {
    CLIMATE_TROPICAL: tank_wagon_default_tropic_switch_gui;
    tank_wagon_oil_switch_gui;
}

switch(FEAT_TRAINS, SELF, tank_wagon_switch, cargo_type_in_veh) {
    CMNT: tank_wagon_cement_switch_gui;
    COLA: tank_wagon_cola_group;
    FZDR: tank_wagon_default_climate_switch;
    MILK: tank_wagon_milk_switch_gui;
    OIL_: tank_wagon_oil_climate_switch;
    PETR: tank_wagon_fueloil_switch_gui;
    RFPR: tank_wagon_refineryproducts_switch_gui;
    WATR: tank_wagon_water_switch_gui;
    tank_wagon_default_climate_switch;
}

switch (FEAT_TRAINS, SELF, tank_wagon_cb_capacity_switch, cargo_type_in_veh) {
    // default 30.000l
    COLA: return 25;
    CMNT: return 15;
    FZDR: return 25;
    MILK: return 25;
    WATR: return 25;
    CB_FAILED;
}

switch (FEAT_TRAINS, SELF, tank_wagon_cb_property_weight_switch, cargo_type_in_veh) {
    // default: 24 tons
    WATR: return 25;
    MILK: return 25;
    CB_FAILED;
}

item(FEAT_TRAINS, tank_wagon) {
    property {
        // We try to simulate the stats of the temperate oil wagon
        name:                           string(STR_NAME_TANK_WAGON);
        REFIT_PROPERTIES_TANK
        WAGON_PROPERTIES_DEFAULT
        WAGON_PROPERTIES_RAIL_DEFAULT
        cost_factor:                    200;
        cargo_capacity:                 30;
        weight:                         24;
        misc_flags:                     bitmask(TRAIN_FLAG_2CC);
    }
    graphics {
        cargo_capacity:     tank_wagon_cb_capacity_switch;
        weight:             tank_wagon_cb_property_weight_switch;
        refit_cost:         tank_refit_cost_switch;

        purchase: tank_wagon_default_climate_switch;
        tank_wagon_switch;
    }
}
