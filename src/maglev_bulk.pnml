/* 
 * ***************************
 * Bulk wagon:
 *
 * coal, iron ore, bauxite, copper ore,
 * gravel, sand, clay, grains, maize, wheat,
 * sulphur, fertilizer, scrap metal,
 * fruit, fruit & vegetables, toffee,
 * sugar, cotton candy, sugar beet,
 * lumber (wood chips)
 * ***************************
 */

switch (FEAT_TRAINS, SELF, maglev_bulk_wagon_cb_capacity_switch, cargo_type_in_veh) {
    // default: 37 units
    FICR: return 32;
    FRUT: return 32;
    FRVG: return 32;
    RSGR: return 27;
    RUBR: return 28;
    SWET: return 32;
    CB_FAILED;
}

switch (FEAT_TRAINS, SELF, maglev_bulk_wagon_cb_property_switch, extra_callback_info1) {
    0x16: bulk_wagon_cb_property_weight_switch; // same weight for all rail types
    0x14: maglev_bulk_wagon_cb_capacity_switch; // set weight
    CB_FAILED;
}

switch (FEAT_TRAINS, SELF, maglev_bulk_wagon_switch, current_callback) {
    VEH_CB_REFITTED_CAPACITY:  maglev_bulk_wagon_cb_capacity_switch;
    VEH_CB_VEHICLE_PROPERTIES: maglev_bulk_wagon_cb_property_switch;
    monorail_bulk_wagon_graphics_switch;
}

item(FEAT_TRAINS, maglev_bulk_wagon) {
    property {
        name:                           string(STR_NAME_BULK_WAGON);
        REFIT_PROPERTIES_BULK
        WAGON_PROPERTIES_DEFAULT
        WAGON_PROPERTIES_MAGLEV_DEFAULT
        cost_factor:                    176;
        callback_flags:                 bitmask(VEH_CBF_REFITTED_CAPACITY);
        cargo_capacity:                 37;
        weight:                         18;
     }
    graphics {
        maglev_bulk_wagon_switch;     // we use the same graphics so far
    }        
}