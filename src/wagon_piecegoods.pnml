/* 
 * ***************************
 * Piece goods wagon:
 *
 * goods, food, paper, fruits, fruit + vegetables,
 * farm supplies, engineering supplies, 
 * manufacturing supplies, wool
 * ***************************
 */

spriteblock(FEAT_TRAINS) {
    spriteset(piecegoods_wagon_default_set, "src/gfx/temperate_railwagons.png") { tmpl_railwagon(0,125) }
    spritegroup piecegoods_wagon_default_group {
        loaded: piecegoods_wagon_default_set;
        loading: piecegoods_wagon_default_set;
    }

    spriteset(piecegoods_wagon_paper_open_set, "src/gfx/arctic_railwagons.png") { tmpl_railwagon(0,200) }
    spriteset(piecegoods_wagon_paper_closed_set, "src/gfx/arctic_railwagons.png") { tmpl_railwagon(0,425) }
    spritegroup piecegoods_wagon_paper_group {
        loaded: piecegoods_wagon_paper_closed_set;
        loading: piecegoods_wagon_paper_open_set;
    }

    spriteset(piecegoods_wagon_fruit_set, "src/gfx/arctic_railwagons.png") { tmpl_railwagon(0,400) }
    spritegroup piecegoods_wagon_fruit_group {
        loaded: piecegoods_wagon_fruit_set;
        loading: piecegoods_wagon_fruit_set;
    }

    spriteset(piecegoods_wagon_livestock_set, "src/gfx/temperate_railwagons.png") { tmpl_railwagon(0,100) }
    spritegroup piecegoods_wagon_livestock_group {
        loaded: piecegoods_wagon_livestock_set;
        loading: piecegoods_wagon_livestock_set;
    }

    spriteset(piecegoods_wagon_toyland_open_set, "src/gfx/toyland_railwagons.png") { tmpl_short_railwagon(0,260) }
    spriteset(piecegoods_wagon_toyland_closed_set, "src/gfx/toyland_railwagons.png") { tmpl_short_railwagon(0,280) }
    spritegroup piecegoods_wagon_toyland_group {
        loaded: piecegoods_wagon_toyland_closed_set;
        loading: piecegoods_wagon_toyland_open_set;
    }

    spriteset(piecegoods_wagon_toys_open_set, "src/gfx/toyland_railwagons.png") { tmpl_short_railwagon(0,300) }
    spriteset(piecegoods_wagon_toys_closed_set, "src/gfx/toyland_railwagons.png") { tmpl_short_railwagon(0,320) }
    spritegroup piecegoods_wagon_toys_group {
        loaded: piecegoods_wagon_toys_closed_set;
        loading: piecegoods_wagon_toys_open_set;
    }
}

switch(FEAT_TRAINS, SELF, piecegoods_wagon_switch, cargo_type_in_veh) {
    BATT: piecegoods_wagon_toyland_group;
    FRUT: piecegoods_wagon_fruit_group;
    FRVG: piecegoods_wagon_fruit_group;
    LVST: piecegoods_wagon_livestock_group;
    PAPR: piecegoods_wagon_paper_group;
    SWET: piecegoods_wagon_toyland_group;
    TOYS: piecegoods_wagon_toys_group;
    piecegoods_wagon_default_group;
}

switch (FEAT_TRAINS, SELF, piecegoods_wagon_cb_capacity_switch, cargo_type_in_veh) {
    FOOD: return 20;
    FRUT: return 20;
    FRVG: return 20;
    return 25;
}

switch (FEAT_TRAINS, SELF, piecegoods_wagon_cb_property_weight_switch, cargo_type_in_veh) {
    FOOD: return 22;
    FRUT: return 22;
    FRVG: return 22;
    GOOD: return 21;
    MILK: return 22;
    return 21;
}

switch (FEAT_TRAINS, SELF, piecegoods_wagon_cb_property_switch, extra_callback_info1) {
    0x16: piecegoods_wagon_cb_property_weight_switch;
    CB_FAILED;
}

switch (FEAT_TRAINS, SELF, piecegoods_wagon_cb_switch, current_callback) {
    VEH_CB_REFITTED_CAPACITY:  piecegoods_wagon_cb_capacity_switch;
    VEH_CB_VEHICLE_PROPERTIES: piecegoods_wagon_cb_property_switch;
    piecegoods_wagon_switch;
}

item(FEAT_TRAINS, piecegoods_wagon) {
    property {
        // We try to simulate the stats of the temperate goods wagon
        name:                           string(STR_NAME_COVERED_PIECE_GOODS_WAGON);
        climates_available:             CLIMATE_ALL;
        refittable_cargo_classes:       CC_PIECE_GOODS | CC_EXPRESS;
        non_refittable_cargo_classes:   CC_PASSENGERS | CC_MAIL | CC_BULK | CC_ARMOURED | CC_LIQUID | CC_OVERSIZED | CC_REFRIGERATED;
        refittable_cargo_types:         bitmask(WOOD, LVST, STEL, SCRP, VEHI, BRCK, BUBL, FOOD, FRUT, FRVG);
        sprite_id:                      SPRITE_ID_NEW_TRAIN;
        introduction_date:              date(1880,1,1);
        model_life:                     255;
        retire_early:                   0;
        vehicle_life:                   30;
        reliability_decay:              0;
        loading_speed:                  10;
        cost_factor:                    190;
        running_cost_factor:            5;
        speed:                          0;
        refit_cost:                     40;
        callback_flags:                 VEH_CBF_REFITTED_CAPACITY;
        track_type:                     RAIL;
        power:                          0;
        running_cost_base:              RUNNING_COST_STEAM;
        cargo_capacity:                 25;
        weight:                         21;
        bitmask_vehicle_info:           0;
    }
    graphics {
        piecegoods_wagon_cb_switch;
    }
}