/*
 * This file is part of OpenGFX+ Trains.
 * OpenGFX+ Trains is free software; you can redistribute it and/or modify it under the terms of the GNU General Public License as published by the Free Software Foundation, version 2.
 * OpenGFX+ Trains is distributed in the hope that it will be useful, but WITHOUT ANY WARRANTY; without even the implied warranty of MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.
 * See the GNU General Public License for more details. You should have received a copy of the GNU General Public License along with OpenGFX+ Trains. If not, see <http://www.gnu.org/licenses/>.
 */

/*
 * ***************************
 * Piece goods wagon:
 *
 * goods, food, paper, fruits, fruit + vegetables,
 * farm supplies, engineering supplies,
 * manufacturing supplies, wool
 * ***************************
 */

MU_8SPRITES_32BPP(piecegoods_wagon_default, "goods", "src/gfx/temperate_railwagons.png", tmpl_railwagon42, 0, 125) // 8 views needed

spriteset(piecegoods_wagon_paper_open_set, "src/gfx/arctic_railwagons.png") { tmpl_railwagon(0,200) }
spriteset(piecegoods_wagon_paper_closed_set, "src/gfx/arctic_railwagons.png") { tmpl_railwagon(0,425) }
spritegroup piecegoods_wagon_paper_group {
	loaded: piecegoods_wagon_paper_closed_set;
	loading: piecegoods_wagon_paper_open_set;
}

spriteset(piecegoods_wagon_fruit_set, "src/gfx/arctic_railwagons.png") { tmpl_railwagon(0,400) }
spritegroup piecegoods_wagon_fruit_group {
	loaded: piecegoods_wagon_fruit_set;
	loading: piecegoods_wagon_fruit_set;
}

MU_8SPRITES_32BPP(piecegoods_wagon_livestock, "livestock", "src/gfx/temperate_railwagons.png", tmpl_railwagon42, 0, 100) // 8 views needed

spriteset(piecegoods_wagon_toyland_open_set, "src/gfx/toyland_railwagons.png") { tmpl_short_railwagon(0,260) }
spriteset(piecegoods_wagon_toyland_closed_set, "src/gfx/toyland_railwagons.png") { tmpl_short_railwagon(0,280) }
spritegroup piecegoods_wagon_toyland_group {
	loaded: piecegoods_wagon_toyland_closed_set;
	loading: piecegoods_wagon_toyland_open_set;
}

spriteset(piecegoods_wagon_toys_open_set, "src/gfx/toyland_railwagons.png") { tmpl_short_railwagon(0,300) }
spriteset(piecegoods_wagon_toys_closed_set, "src/gfx/toyland_railwagons.png") { tmpl_short_railwagon(0,320) }
spritegroup piecegoods_wagon_toys_group {
	loaded: piecegoods_wagon_toys_closed_set;
	loading: piecegoods_wagon_toys_open_set;
}

switch(FEAT_TRAINS, SELF, piecegoods_wagon_switch, cargo_type_in_veh) {
    BATT: piecegoods_wagon_toyland_group;
    FRUT: piecegoods_wagon_fruit_group;
    FRVG: piecegoods_wagon_fruit_group;
    LVST: piecegoods_wagon_livestock_switch_gui;
    PAPR: piecegoods_wagon_paper_group;
    SWET: piecegoods_wagon_toyland_group;
    TOYS: piecegoods_wagon_toys_group;
    piecegoods_wagon_default_switch_gui;
}

switch (FEAT_TRAINS, SELF, piecegoods_wagon_capacity_switch, cargo_type_in_veh) {
    // default: 25 units
    FOOD: return 20;
    FRUT: return 20;
    FRVG: return 20;
    PAPR: return 20;
    TOYS: return 20;
    CB_FAILED;
}

switch (FEAT_TRAINS, SELF, piecegoods_wagon_weight_switch, cargo_type_in_veh) {
    // default: 21 tons
    FOOD: return 22;
    FRUT: return 22;
    FRVG: return 22;
    MILK: return 22;
    PAPR: return 18;
    TOYS: return 21;
    CB_FAILED;
}

item(FEAT_TRAINS, piecegoods_wagon) {
    property {
        // We try to simulate the stats of the temperate goods wagon
        name:                           string(STR_NAME_COVERED_PIECE_GOODS_WAGON);
        REFIT_PROPERTIES_PIECE_GOODS
        WAGON_PROPERTIES_DEFAULT
        WAGON_PROPERTIES_RAIL_DEFAULT
        cost_factor:                    190;
        cargo_capacity:                 25;
        weight:                         21;
    }
    graphics {
        weight:         piecegoods_wagon_weight_switch;
        cargo_capacity: piecegoods_wagon_capacity_switch;
        refit_cost:     piecegoods_refit_cost_switch;

        purchase:   piecegoods_wagon_default_switch_gui;
        piecegoods_wagon_switch;
    }
}
