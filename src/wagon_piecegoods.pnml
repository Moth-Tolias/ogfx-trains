/*
 * ***************************
 * Piece goods wagon:
 *
 * goods, food, paper, fruits, fruit + vegetables,
 * farm supplies, engineering supplies,
 * manufacturing supplies, wool
 * ***************************
 */


spriteset(piecegoods_wagon_default_set, "src/gfx/temperate_railwagons.png") { tmpl_railwagon(0,125) tmpl_railwagon(0,125) } // 8 views needed for 32bpp

/* OpenGFX+ */
/* Define the 32bpp sprite set for normal zoom */
alternative_sprites(piecegoods_wagon_default_set, ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP) {
    template_train_opengfxp32(1, "goods", "1")
}

/* OpenGFX+ */
/* Define the 32bpp sprite set for 2X zoom in */
alternative_sprites(piecegoods_wagon_default_set, ZOOM_LEVEL_IN_2X, BIT_DEPTH_32BPP) {
    template_train_opengfxp32(2, "goods", "2")
}

/* OpenGFX+ */
/* Define the 32bpp sprite set for 4X zoom in */
alternative_sprites(piecegoods_wagon_default_set, ZOOM_LEVEL_IN_4X, BIT_DEPTH_32BPP) {
    template_train_opengfxp32(4, "goods", "4")
}

/* OpenGFX+ */
/* Define the 8bpp sprite set for the GUI menus */
spriteset(piecegoods_wagon_default_set_gui, "src/gfx/temperate_railwagons.png") {
	tmpl_railwagon(0,125) tmpl_railwagon(0,125)               // 8 views needed for 32bpp
}

/* OpenGFX+ */
/* Define the 32bpp sprite set for the GUI menus */
alternative_sprites(piecegoods_wagon_default_set_gui, ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP) {
    template_train_gui_opengfxp32("goods")
}

/* OpenGFX+ */
/* Define the sprite group for the GUI menus */
spritegroup piecegoods_wagon_default_group_gui {
	loading: piecegoods_wagon_default_set_gui;
	loaded: piecegoods_wagon_default_set_gui;
}

spritegroup piecegoods_wagon_default_group {
	loaded: piecegoods_wagon_default_set;
	loading: piecegoods_wagon_default_set;
}

spriteset(piecegoods_wagon_paper_open_set, "src/gfx/arctic_railwagons.png") { tmpl_railwagon(0,200) }
spriteset(piecegoods_wagon_paper_closed_set, "src/gfx/arctic_railwagons.png") { tmpl_railwagon(0,425) }
spritegroup piecegoods_wagon_paper_group {
	loaded: piecegoods_wagon_paper_closed_set;
	loading: piecegoods_wagon_paper_open_set;
}

spriteset(piecegoods_wagon_fruit_set, "src/gfx/arctic_railwagons.png") { tmpl_railwagon(0,400) }
spritegroup piecegoods_wagon_fruit_group {
	loaded: piecegoods_wagon_fruit_set;
	loading: piecegoods_wagon_fruit_set;
}

spriteset(piecegoods_wagon_livestock_set, "src/gfx/temperate_railwagons.png") { tmpl_railwagon(0,100) tmpl_railwagon(0,100) } // 8 views needed for 32bpp

/* OpenGFX+ */
/* Define the 32bpp sprite set for normal zoom */
alternative_sprites(piecegoods_wagon_livestock_set, ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP) {
    template_train_opengfxp32(1, "livestock", "1")
}

/* OpenGFX+ */
/* Define the 32bpp sprite set for 2X zoom in */
alternative_sprites(piecegoods_wagon_livestock_set, ZOOM_LEVEL_IN_2X, BIT_DEPTH_32BPP) {
    template_train_opengfxp32(2, "livestock", "2")
}

/* OpenGFX+ */
/* Define the 32bpp sprite set for 4X zoom in */
alternative_sprites(piecegoods_wagon_livestock_set, ZOOM_LEVEL_IN_4X, BIT_DEPTH_32BPP) {
    template_train_opengfxp32(4, "livestock", "4")
}

/* OpenGFX+ */
/* Define the 8bpp sprite set for the GUI menus */
spriteset(piecegoods_wagon_livestock_set_gui, "src/gfx/temperate_railwagons.png") {
	tmpl_railwagon(0,100) tmpl_railwagon(0,100)               // 8 views needed for 32bpp
}

/* OpenGFX+ */
/* Define the 32bpp sprite set for the GUI menus */
alternative_sprites(piecegoods_wagon_livestock_set_gui, ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP) {
    template_train_gui_opengfxp32("livestock")
}

/* OpenGFX+ */
/* Define the sprite group for the GUI menus */
spritegroup piecegoods_wagon_livestock_group_gui {
	loading: piecegoods_wagon_livestock_set_gui;
	loaded: piecegoods_wagon_livestock_set_gui;
}

spritegroup piecegoods_wagon_livestock_group {
	loaded: piecegoods_wagon_livestock_set;
	loading: piecegoods_wagon_livestock_set;
}

spriteset(piecegoods_wagon_toyland_open_set, "src/gfx/toyland_railwagons.png") { tmpl_short_railwagon(0,260) }
spriteset(piecegoods_wagon_toyland_closed_set, "src/gfx/toyland_railwagons.png") { tmpl_short_railwagon(0,280) }
spritegroup piecegoods_wagon_toyland_group {
	loaded: piecegoods_wagon_toyland_closed_set;
	loading: piecegoods_wagon_toyland_open_set;
}

spriteset(piecegoods_wagon_toys_open_set, "src/gfx/toyland_railwagons.png") { tmpl_short_railwagon(0,300) }
spriteset(piecegoods_wagon_toys_closed_set, "src/gfx/toyland_railwagons.png") { tmpl_short_railwagon(0,320) }
spritegroup piecegoods_wagon_toys_group {
	loaded: piecegoods_wagon_toys_closed_set;
	loading: piecegoods_wagon_toys_open_set;
}


/* OpenGFX+ */
/* Define the switch for the GUI menus */
switch(FEAT_TRAINS, SELF, piecegoods_wagon_default_switch_gui, (extra_callback_info1 & 0xFF)) {
    0x10: piecegoods_wagon_default_group_gui;
    0x11: piecegoods_wagon_default_group_gui;
    0x12: piecegoods_wagon_default_group_gui;
    0x20: piecegoods_wagon_default_group_gui;
    0x21: piecegoods_wagon_default_group_gui;
    piecegoods_wagon_default_group;
}

/* OpenGFX+ */
/* Define the switch for the GUI menus */
switch(FEAT_TRAINS, SELF, piecegoods_wagon_livestock_switch_gui, (extra_callback_info1 & 0xFF)) {
    0x10: piecegoods_wagon_livestock_group_gui;
    0x11: piecegoods_wagon_livestock_group_gui;
    0x12: piecegoods_wagon_livestock_group_gui;
    0x20: piecegoods_wagon_livestock_group_gui;
    0x21: piecegoods_wagon_livestock_group_gui;
    piecegoods_wagon_livestock_group;
}

switch(FEAT_TRAINS, SELF, piecegoods_wagon_switch, cargo_type_in_veh) {
    BATT: piecegoods_wagon_toyland_group;
    FRUT: piecegoods_wagon_fruit_group;
    FRVG: piecegoods_wagon_fruit_group;
    LVST: piecegoods_wagon_livestock_switch_gui;
    PAPR: piecegoods_wagon_paper_group;
    SWET: piecegoods_wagon_toyland_group;
    TOYS: piecegoods_wagon_toys_group;
    piecegoods_wagon_default_switch_gui;
}

switch (FEAT_TRAINS, SELF, piecegoods_wagon_capacity_switch, cargo_type_in_veh) {
    // default: 25 units
    FOOD: return 20;
    FRUT: return 20;
    FRVG: return 20;
    PAPR: return 20;
    TOYS: return 20;
    CB_FAILED;
}

switch (FEAT_TRAINS, SELF, piecegoods_wagon_weight_switch, cargo_type_in_veh) {
    // default: 21 tons
    FOOD: return 22;
    FRUT: return 22;
    FRVG: return 22;
    MILK: return 22;
    PAPR: return 18;
    TOYS: return 21;
    CB_FAILED;
}

// Removing piece goods from a wagon and putting in another piece goods can be done
// at no extra costs in a station. Fruits etc are packed into convenient boxes when
// carried in this wagon type.
switch (FEAT_TRAINS, SELF, piecegoods_wagon_refit_cost_switch, 1) {
	return 0 | CB_RESULT_AUTOREFIT;
}

item(FEAT_TRAINS, piecegoods_wagon) {
    property {
        // We try to simulate the stats of the temperate goods wagon
        name:                           string(STR_NAME_COVERED_PIECE_GOODS_WAGON);
        REFIT_PROPERTIES_PIECE_GOODS
        WAGON_PROPERTIES_DEFAULT
        WAGON_PROPERTIES_RAIL_DEFAULT
        cost_factor:                    190;
        cargo_capacity:                 25;
        weight:                         21;
        misc_flags:                     bitmask(TRAIN_FLAG_2CC);
    }
    graphics {
        weight:         piecegoods_wagon_weight_switch;
        cargo_capacity: piecegoods_wagon_capacity_switch;
        refit_cost:     piecegoods_wagon_refit_cost_switch;

        purchase:   piecegoods_wagon_default_switch_gui;
        piecegoods_wagon_switch;
    }
}
