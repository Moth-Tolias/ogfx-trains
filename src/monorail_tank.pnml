spriteset(monorail_tank_oil_arctic_set, "src/gfx/arctic_monolev.png") { tmpl_short_railwagon(0,140) }
spriteset(monorail_tank_oil_tropic_set, "src/gfx/tropic_monolev.png") { tmpl_short_railwagon(0,100) }
spriteset(monorail_tank_oil_temperate_set, "src/gfx/temperate_monolev.png") { tmpl_short_railwagon(0,140) }
spritegroup monorail_tank_oil_temperate_group {
	loaded: monorail_tank_oil_temperate_set;
	loading: monorail_tank_oil_temperate_set;
}
spritegroup monorail_tank_oil_tropic_group {
	loaded: monorail_tank_oil_tropic_set;
	loading: monorail_tank_oil_tropic_set;
}
spritegroup monorail_tank_oil_arctic_group {
	loaded: monorail_tank_oil_arctic_set;
	loading: monorail_tank_oil_arctic_set;
}

spriteset(monorail_tank_water_set, "src/gfx/tropic_monolev.png") { tmpl_short_railwagon(0,140) }
spritegroup monorail_tank_water_group {
	loaded: monorail_tank_water_set;
	loading: monorail_tank_water_set;
}

switch(FEAT_TRAINS, SELF, monorail_tank_oil_climate_switch, climate) {
	CLIMATE_ARCTIC: monorail_tank_oil_arctic_group;
	CLIMATE_TROPIC: monorail_tank_oil_tropic_group;
	monorail_tank_oil_temperate_group;
}

switch(FEAT_TRAINS, SELF, monorail_tank_graphics_switch, cargo_type_in_veh) {
	OIL_: monorail_tank_oil_climate_switch;
	WATR: monorail_tank_water_group;
	tank_wagon_switch;                   // fall back to normal rail tank wagon
}

switch (FEAT_TRAINS, SELF, monorail_tank_switch, current_callback) {
    VEH_CB_REFITTED_CAPACITY:  tank_wagon_cb_capacity_switch; // use rail wagon properties
    VEH_CB_VEHICLE_PROPERTIES: tank_wagon_cb_property_switch; // use rail wagon properties
    monorail_tank_graphics_switch;
}

item(FEAT_TRAINS, monorail_tank_wagon) {
    property {
        name:                           string(STR_NAME_TANK_WAGON);
        climates_available:             ALL_CLIMATES;
        refittable_cargo_classes:       bitmask(CC_LIQUID);
        non_refittable_cargo_classes:   bitmask(CC_PASSENGERS, CC_MAIL, CC_ARMOURED, CC_PIECE_GOODS);
        refittable_cargo_types:         bitmask(CMNT, FZDR);
        sprite_id:                      SPRITE_ID_NEW_TRAIN;
        introduction_date:              date(1980,1,1);
        model_life:                     50;
        retire_early:                   0;
        vehicle_life:                   20;
        reliability_decay:              20;
        loading_speed:                  5;
        cost_factor:                    200;
        running_cost_factor:            5;
        speed:                          0;
        refit_cost:                     40;
        callback_flags:                 bitmask(VEH_CBF_REFITTED_CAPACITY);
        track_type:                     MONO;
        power:                          0;
        running_cost_base:              RUNNING_COST_STEAM;
        cargo_capacity:                 30;
        weight:                         24;
        bitmask_vehicle_info:           0;
    }
    graphics {
        monorail_tank_switch;
    }
}