/*
 * ***************************
 * Flatbed wagon:
 *
 * wood, lumber, metal, steel, glass,
 * goods, manufacturing supplies,
 * farm supplies, engineering supplies,
 * batteries, fizzy drinks, bubbles
 * ***************************
 */

// default: container
spriteset(flatbed_wagon_container_empty_set, "src/gfx/flatbed/rail_flatbed_container_temperate.png") { tmpl_railwagon(0, 8) }
spriteset(flatbed_wagon_container_full_set, "src/gfx/flatbed/rail_flatbed_container_temperate.png") { tmpl_railwagon(126, 8) }
spritegroup flatbed_wagon_container_group {
	loaded: [flatbed_wagon_container_empty_set, flatbed_wagon_container_full_set];
	loading: [flatbed_wagon_container_empty_set, flatbed_wagon_container_full_set];
}

// BATT
spriteset(flatbed_wagon_battery_set, "src/gfx/toyland_railwagons.png") { tmpl_short_railwagon(0,400) }
spriteset(flatbed_wagon_battery_empty_set, "src/gfx/toyland_railwagons.png") { tmpl_short_railwagon(0,380) }
spritegroup flatbed_wagon_battery_group {
	loaded: [flatbed_wagon_battery_empty_set, flatbed_wagon_battery_set];
	loading: [flatbed_wagon_battery_empty_set, flatbed_wagon_battery_set];
}

// BUBL
spriteset(flatbed_wagon_bubbles_set, "src/gfx/toyland_railwagons.png") { tmpl_short_railwagon(0,80) }
spriteset(flatbed_wagon_bubbles_empty_set, "src/gfx/toyland_railwagons.png") { tmpl_short_railwagon(0,60) }
spritegroup flatbed_wagon_bubbles_group {
	loaded: [flatbed_wagon_bubbles_empty_set, flatbed_wagon_bubbles_set];
	loading: [flatbed_wagon_bubbles_empty_set, flatbed_wagon_bubbles_set];
}

// ENSP (vehicles)
spriteset(flatbed_wagon_engineeringsupplies_empty_set, "src/gfx/flatbed/ensp/rail_flatbed_ensp_empty_wagoncc1.png") { tmpl_railwagon8_normal(0,0) }
spriteset(flatbed_wagon_engineeringsupplies_vehicle1_set, "src/gfx/flatbed/ensp/rail_flatbed_ensp_vehicles_wagoncc1a.png") { tmpl_railwagon8_normal(0,0) }
spritegroup flatbed_wagon_engineeringsupplies_vehicle1_group {
	loaded: [flatbed_wagon_engineeringsupplies_empty_set, flatbed_wagon_engineeringsupplies_vehicle1_set];
	loading: [flatbed_wagon_engineeringsupplies_empty_set, flatbed_wagon_engineeringsupplies_vehicle1_set];
}
spriteset(flatbed_wagon_engineeringsupplies_vehicle2_set, "src/gfx/flatbed/ensp/rail_flatbed_ensp_vehicles_wagoncc1b.png") { tmpl_railwagon8_normal(0,0) }
spritegroup flatbed_wagon_engineeringsupplies_vehicle2_group {
	loaded: [flatbed_wagon_engineeringsupplies_empty_set, flatbed_wagon_engineeringsupplies_vehicle2_set];
	loading: [flatbed_wagon_engineeringsupplies_empty_set, flatbed_wagon_engineeringsupplies_vehicle2_set];
}

// FMSP: see VEHI

// FZDR
spriteset(flatbed_wagon_fizzy_set, "src/gfx/toyland_railwagons.png") { tmpl_short_railwagon(0,360) }
spriteset(flatbed_wagon_fizzy_empty_set, "src/gfx/toyland_railwagons.png") { tmpl_short_railwagon(0,340) }
spritegroup flatbed_wagon_fizzy_group {
	loaded: [flatbed_wagon_fizzy_empty_set, flatbed_wagon_fizzy_set];
	loading: [flatbed_wagon_fizzy_empty_set, flatbed_wagon_fizzy_set];
}

// GOOD (container)
spriteset(flatbed_wagon_goods_empty_set, "src/gfx/flatbed/container/rail_flatbed_container_empty_NoWagonCC_2.png") { tmpl_railwagon(0,0) }
spriteset(flatbed_wagon_goods_container1_set, "src/gfx/flatbed/container/rail_flatbed_container_full_NoWagonCC_2a.png") { tmpl_railwagon(0,0) }
spritegroup flatbed_wagon_goods_1a_group {
	loaded: [flatbed_wagon_goods_empty_set, flatbed_wagon_goods_container1_set];
	loading: [flatbed_wagon_goods_empty_set, flatbed_wagon_goods_container1_set];
}

// PAPR
spriteset(flatbed_wagon_paper_empty_set, "src/gfx/flatbed/rail_flatbed_paper_temperate.png") { tmpl_railwagon(0, 0) }
spriteset(flatbed_wagon_paper_full_set, "src/gfx/flatbed/rail_flatbed_paper_temperate.png") { tmpl_railwagon(126, 0) }
spritegroup flatbed_wagon_paper_group {
	loaded: [flatbed_wagon_paper_empty_set, flatbed_wagon_paper_full_set];
	loading: [flatbed_wagon_paper_empty_set, flatbed_wagon_paper_full_set];
}

// RFPR (barrels)
spriteset(flatbed_wagon_refineryproducts_empty_set, "src/gfx/flatbed/rail_flatbed_refineryproducts_temperate.png") { tmpl_railwagon(0, 10) }
spriteset(flatbed_wagon_refineryproducts_full_set, "src/gfx/flatbed/rail_flatbed_refineryproducts_temperate.png") { tmpl_railwagon(126, 10) }
spritegroup flatbed_wagon_refineryproducts_group {
	loaded: [flatbed_wagon_refineryproducts_empty_set, flatbed_wagon_refineryproducts_full_set];
	loading: [flatbed_wagon_refineryproducts_empty_set, flatbed_wagon_refineryproducts_full_set];
}

// PETR (barrels)
spriteset(flatbed_wagon_fueloil_empty_set, "src/gfx/flatbed/rail_flatbed_fueloil_temperate.png") { tmpl_railwagon(0, 0) }
spriteset(flatbed_wagon_fueloil_full_set, "src/gfx/flatbed/rail_flatbed_fueloil_temperate.png") { tmpl_railwagon(126, 0) }
spritegroup flatbed_wagon_fueloil_group {
	loaded: [flatbed_wagon_fueloil_empty_set, flatbed_wagon_fueloil_full_set];
	loading: [flatbed_wagon_fueloil_empty_set, flatbed_wagon_fueloil_full_set];
}

// STEL
SPRITEGROUP_3STAGES_NOLOADING(flatbed_wagon_steel_rolls1, 
    "src/gfx/flatbed/steel/SteelFlatbeds1.png", "src/gfx/flatbed/steel/SteelFlatbeds1a1.png", 
    "src/gfx/flatbed/steel/SteelFlatbeds1b1.png", tmpl_railwagon, 0, 0, 0, 0)
SPRITEGROUP_3STAGES_NOLOADING(flatbed_wagon_steel_rolls2, 
    "src/gfx/flatbed/steel/SteelFlatbeds1.png", "src/gfx/flatbed/steel/SteelFlatbeds1a2.png", 
    "src/gfx/flatbed/steel/SteelFlatbeds1b2.png", tmpl_railwagon, 0, 0, 0, 0)

// VEHI (vehicles)
spriteset(flatbed_wagon_vehicles_empty_set, "src/gfx/flatbed/rail_flatbed_vehicles_temperate.png") { tmpl_railwagon42(0, 7) }
spriteset(flatbed_wagon_vehicles_full_set, "src/gfx/flatbed/rail_flatbed_vehicles_temperate.png") { tmpl_railwagon8_high(126, 7) }
spritegroup flatbed_wagon_vehicles_group {
	loaded: [flatbed_wagon_vehicles_empty_set, flatbed_wagon_vehicles_full_set];
	loading: [flatbed_wagon_vehicles_empty_set, flatbed_wagon_vehicles_full_set];
}
// WDPR
spriteset(flatbed_wagon_woodproducts_empty_set, "src/gfx/flatbed/rail_flatbed_woodproducts_temperate.png") { tmpl_railwagon(0, 0) }
spriteset(flatbed_wagon_woodproducts_full_set, "src/gfx/flatbed/rail_flatbed_woodproducts_temperate.png") { tmpl_railwagon(126, 0) }
spritegroup flatbed_wagon_woodproducts_group {
	loaded: [flatbed_wagon_woodproducts_empty_set, flatbed_wagon_woodproducts_full_set];
	loading: [flatbed_wagon_woodproducts_empty_set, flatbed_wagon_woodproducts_full_set];
}

// WOOD
SPRITEGROUP_4STAGES_NOLOADING(flatbed_wagon_wood, 
    "src/gfx/flatbed/wood/WoodFlatbeds1.png", "src/gfx/flatbed/wood/WoodFlatbeds1a.png", 
    "src/gfx/flatbed/wood/WoodFlatbeds1b.png", "src/gfx/flatbed/wood/WoodFlatbeds1c.png", 
    tmpl_railwagon, 0, 0, 0, 0, 0)

random_switch (FEAT_TRAINS, SELF, flatbed_wagon_engineeringsupplies_switch, bitmask(TRIGGER_VEHICLE_NEW_LOAD)) {
    3: flatbed_wagon_engineeringsupplies_vehicle1_group;
    1: flatbed_wagon_engineeringsupplies_vehicle2_group;
}

random_switch (FEAT_TRAINS, SELF, flatbed_wagon_steel_switch, bitmask(TRIGGER_VEHICLE_NEW_LOAD)) {
    1: flatbed_wagon_steel_rolls1_group;
    1: flatbed_wagon_steel_rolls2_group;
}

random_switch (FEAT_TRAINS, SELF, flatbed_wagon_random_colour_switch, bitmask(TRIGGER_VEHICLE_NEW_LOAD)) {
    1: return 0x307;
    1: return 0x308;
    1: return 0x309;
    1: return 0x30A;
    1: return 0x30B;
    1: return 0x30C;
    1: return 0x30D;
    1: return 0x30E;
    1: return 0x30F;
    1: return 0x310;
    1: return 0x311;
    1: return 0x312;
    1: return 0x313;
    1: return 0x314;
    1: return 0x315;
    1: return 0x316;
    48: return 0x4000;
}

switch (FEAT_TRAINS, SELF, flatbed_wagon_switch, cargo_type_in_veh) {
    BATT: flatbed_wagon_battery_group;
    BUBL: flatbed_wagon_bubbles_group;
    ENSP: flatbed_wagon_engineeringsupplies_switch;
    FMSP: flatbed_wagon_vehicles_group;
    FZDR: flatbed_wagon_fizzy_group;
    GOOD: flatbed_wagon_goods_1a_group;
    PAPR: flatbed_wagon_paper_group;
    PETR: flatbed_wagon_fueloil_group;
    RFPR: flatbed_wagon_refineryproducts_group;
    STEL: flatbed_wagon_steel_switch;
    VEHI: flatbed_wagon_vehicles_group;
    WDPR: flatbed_wagon_woodproducts_group;
    WOOD: flatbed_wagon_wood_group;
    flatbed_wagon_container_group;
}

switch (FEAT_TRAINS, SELF, flatbed_wagon_cb_capacity_switch, cargo_type_in_veh) {
    BATT: return 22;
    ENSP: return 25;
    FMSP: return 25;
    FZDR: return 25;
    MNSP: return 25;
    PAPR: return 20;
    PETR: return 12;
    RFPR: return 12;
    STEL: return 20;
    VEHI: return 15;
    return 30;
}

switch (FEAT_TRAINS, SELF, flatbed_wagon_cb_property_weight_switch, cargo_type_in_veh) {
    BATT: return 18;
    BUBL: return 21;
    FZDR: return 18;
    GOOD: return 18;
    PAPR: return 18;
    PLAS: return 18;
    PLST: return 18;
    SCRP: return 18;
    STEL: return 18;
    VEHI: return 18;
    return 16;
}

switch (FEAT_TRAINS, SELF, flatbed_wagon_cb_property_switch, extra_callback_info1) {
    0x16: flatbed_wagon_cb_property_weight_switch;
    CB_FAILED;
}

switch (FEAT_TRAINS, SELF, flatbed_wagon_cb_cargo_suffix_switch, cargo_type_in_veh) {
    FMSP: return string(STR_FLATBED_WAGON_CARGO_SUBTYPE_TRACTORS);
    ENSP: return string(STR_FLATBED_WAGON_CARGO_SUBTYPE_MACHINERY);
    GOOD: return string(STR_FLATBED_WAGON_CARGO_SUBTYPE_CONTAINER);
    PETR: return string(STR_FLATBED_WAGON_CARGO_SUBTYPE_BARRELS);
    RFPR: return string(STR_FLATBED_WAGON_CARGO_SUBTYPE_BARRELS);
    VEHI: return string(STR_FLATBED_WAGON_CARGO_SUBTYPE_TRACTORS);
    CB_FAILED;
}

switch (FEAT_TRAINS, SELF, flatbed_wagon_cb_colour_switch, cargo_type_in_veh) {
    GOOD: flatbed_wagon_random_colour_switch;
    CB_FAILED;
}

switch (FEAT_TRAINS, SELF, flatbed_wagon_cb_switch, current_callback) {
    VEH_CB_REFITTED_CAPACITY:  flatbed_wagon_cb_capacity_switch;
    VEH_CB_VEHICLE_PROPERTIES: flatbed_wagon_cb_property_switch;
    VEH_CB_CARGO_SUFFIX: flatbed_wagon_cb_cargo_suffix_switch;
    VEH_CB_COLOUR_MAPPING: flatbed_wagon_cb_colour_switch;
    0x01: flatbed_wagon_cb_colour_switch;
    flatbed_wagon_switch;
}

item(FEAT_TRAINS, flatbed_wagon) {
    property {
        // We try to simulate the stats of the temperate wood wagon
        name:                           string(STR_NAME_FLATBED_WAGON);
        REFIT_PROPERTIES_FLATBED
        WAGON_PROPERTIES_DEFAULT
        WAGON_PROPERTIES_RAIL_DEFAULT
        cost_factor:                    181;
        callback_flags:                 bitmask(VEH_CBF_REFITTED_CAPACITY, VEH_CBF_CARGO_SUFFIX, VEH_CBF_COLOUR_MAPPING);
        cargo_capacity:                 30; // Usually units of goods which fit onto it.
        weight:                         16;
    }
    graphics {
        flatbed_wagon_cb_switch;
    }
}
