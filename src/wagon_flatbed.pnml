/* 
 * ***************************
 * Flatbed wagon:
 *
 * wood, lumber, metal, steel, glass,
 * goods, manufacturing supplies,
 * farm supplies, engineering supplies,
 * batteries, fizzy drinks, bubbles
 * ***************************
 */

spriteset(flatbed_wagon_default_set, "src/gfx/molace_wagons.png") { tmpl_railwagon(127,762) }
spriteset(flatbed_wagon_default_empty_set, "src/gfx/molace_wagons.png") { tmpl_railwagon(0,762) }
spritegroup flatbed_wagon_default_group {
	loaded: [flatbed_wagon_default_empty_set, flatbed_wagon_default_set];
	loading: [flatbed_wagon_default_empty_set, flatbed_wagon_default_set];
}

spriteset(flatbed_wagon_battery_set, "src/gfx/toyland_railwagons.png") { tmpl_short_railwagon(0,400) }
spriteset(flatbed_wagon_battery_empty_set, "src/gfx/toyland_railwagons.png") { tmpl_short_railwagon(0,380) }
spritegroup flatbed_wagon_battery_group {
	loaded: [flatbed_wagon_battery_empty_set, flatbed_wagon_battery_set];
	loading: [flatbed_wagon_battery_empty_set, flatbed_wagon_battery_set];
}

spriteset(flatbed_wagon_bubbles_set, "src/gfx/toyland_railwagons.png") { tmpl_short_railwagon(0,80) }
spriteset(flatbed_wagon_bubbles_empty_set, "src/gfx/toyland_railwagons.png") { tmpl_short_railwagon(0,60) }
spritegroup flatbed_wagon_bubbles_group {
	loaded: [flatbed_wagon_bubbles_empty_set, flatbed_wagon_bubbles_set];
	loading: [flatbed_wagon_bubbles_empty_set, flatbed_wagon_bubbles_set];
}

spriteset(flatbed_wagon_fizzy_set, "src/gfx/toyland_railwagons.png") { tmpl_short_railwagon(0,360) }
spriteset(flatbed_wagon_fizzy_empty_set, "src/gfx/toyland_railwagons.png") { tmpl_short_railwagon(0,340) }
spritegroup flatbed_wagon_fizzy_group {
	loaded: [flatbed_wagon_fizzy_empty_set, flatbed_wagon_fizzy_set];
	loading: [flatbed_wagon_fizzy_empty_set, flatbed_wagon_fizzy_set];
}

spriteset(flatbed_wagon_paper_set, "src/gfx/molace_wagons.png") { tmpl_railwagon(127,722) }
spriteset(flatbed_wagon_paper_empty_set, "src/gfx/molace_wagons.png") { tmpl_railwagon(0,722) }
spritegroup flatbed_wagon_paper_group {
	loaded: [flatbed_wagon_paper_empty_set, flatbed_wagon_paper_set];
	loading: [flatbed_wagon_paper_empty_set, flatbed_wagon_paper_set];
}

spriteset(flatbed_wagon_steel_set, "src/gfx/temperate_railwagons.png") { tmpl_railwagon(0,325) }
spriteset(flatbed_wagon_steel_empty_set, "src/gfx/temperate_railwagons.png") { tmpl_railwagon(0,200) }
spritegroup flatbed_wagon_steel_group {
	loaded: [flatbed_wagon_steel_empty_set, flatbed_wagon_steel_set];
	loading: [flatbed_wagon_steel_empty_set, flatbed_wagon_steel_set];
}

spriteset(flatbed_wagon_wood_set, "src/gfx/molace_wagons.png") { tmpl_railwagon(127, 642) }
spriteset(flatbed_wagon_wood_empty_set, "src/gfx/molace_wagons.png") { tmpl_railwagon(0, 642) }
spritegroup flatbed_wagon_wood_group {
	loaded: [flatbed_wagon_wood_empty_set, flatbed_wagon_wood_set];
	loading: [flatbed_wagon_wood_empty_set, flatbed_wagon_wood_set];
}

spriteset(flatbed_wagon_woodproducts_set, "src/gfx/molace_wagons.png") { tmpl_railwagon(127,802) }
spriteset(flatbed_wagon_woodproducts_empty_set, "src/gfx/molace_wagons.png") { tmpl_railwagon(0,802) }
spritegroup flatbed_wagon_woodproducts_group {
	loaded: [flatbed_wagon_woodproducts_empty_set, flatbed_wagon_woodproducts_set];
	loading: [flatbed_wagon_woodproducts_empty_set, flatbed_wagon_woodproducts_set];
}
 
switch (FEAT_TRAINS, SELF, flatbed_wagon_switch, cargo_type_in_veh) {
    BATT: flatbed_wagon_battery_group;
    BUBL: flatbed_wagon_bubbles_group;
    FZDR: flatbed_wagon_fizzy_group;
    PAPR: flatbed_wagon_paper_group;
    STEL: flatbed_wagon_steel_group;
    WOOD: flatbed_wagon_wood_group;
    WDPR: flatbed_wagon_woodproducts_group;
    flatbed_wagon_default_group;
}

switch (FEAT_TRAINS, SELF, flatbed_wagon_cb_capacity_switch, cargo_type_in_veh) {
    BATT: return 22;
    ENSP: return 25;
    FMSP: return 25;
    FZDR: return 25;
    MNSP: return 25;
    PAPR: return 20;
    STEL: return 20;
    VEHI: return 15;
    return 30;
}

switch (FEAT_TRAINS, SELF, flatbed_wagon_cb_property_weight_switch, cargo_type_in_veh) {
    BATT: return 18;
    BUBL: return 21;
    FZDR: return 18;
    GOOD: return 18;
    PAPR: return 18;
    PLAS: return 18;
    PLST: return 18;
    SCRP: return 18;
    STEL: return 18;
    VEHI: return 18;
    return 16;
}

switch (FEAT_TRAINS, SELF, flatbed_wagon_cb_property_switch, extra_callback_info1) {
    0x16: flatbed_wagon_cb_property_weight_switch;
    CB_FAILED;
}

switch (FEAT_TRAINS, SELF, flatbed_wagon_cb_switch, current_callback) {
    VEH_CB_REFITTED_CAPACITY:  flatbed_wagon_cb_capacity_switch;
    VEH_CB_VEHICLE_PROPERTIES: flatbed_wagon_cb_property_switch;
    flatbed_wagon_switch;
}

item(FEAT_TRAINS, flatbed_wagon) {
    property {
        // We try to simulate the stats of the temperate wood wagon
        name:                           string(STR_NAME_FLATBED_WAGON);
        climates_available:             ALL_CLIMATES;
        refittable_cargo_classes:       bitmask(CC_PIECE_GOODS, CC_EXPRESS);
        non_refittable_cargo_classes:   bitmask(CC_REFRIGERATED);
        refittable_cargo_types:         bitmask(LVST, WOOL, SCRP, TOYS, WDPR);
        sprite_id:                      SPRITE_ID_NEW_TRAIN;
        introduction_date:              date(1925,1,1);
        model_life:                     50;
        retire_early:                   0;
        vehicle_life:                   20;
        reliability_decay:              20;
        loading_speed:                  5;
        cost_factor:                    181;
        running_cost_factor:            5;
        speed:                          0;
        refit_cost:                     40;
        callback_flags:                 bitmask(VEH_CBF_REFITTED_CAPACITY);
        track_type:                     RAIL;
        power:                          0;
        running_cost_base:              RUNNING_COST_STEAM;
        cargo_capacity:                 30; // Usually units of goods which fit onto it.
        weight:                         16;
        bitmask_vehicle_info:           0;
    }
    graphics {
        flatbed_wagon_cb_switch;
    }
}