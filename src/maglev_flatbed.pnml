/* 
 * ***************************
 * Flatbed wagon:
 *
 * wood, lumber, metal, steel, glass,
 * goods, manufacturing supplies,
 * farm supplies, engineering supplies,
 * batteries, fizzy drinks, bubbles
 * ***************************
 */
switch (FEAT_TRAINS, SELF, maglev_flatbed_wagon_cb_capacity_switch, cargo_type_in_veh) {
    BATT: return 29;
    ENSP: return 32;
    FMSP: return 32;
    FZDR: return 32;
    MNSP: return 32;
    PAPR: return 27;
    STEL: return 27;
    VEHI: return 22;
    return 37;
}

switch (FEAT_TRAINS, SELF, maglev_flatbed_wagon_cb_property_switch, extra_callback_info1) {
    0x16: flatbed_wagon_cb_property_weight_switch; // weight is independent of railtype, use rail wagon
    0x14: maglev_flatbed_wagon_cb_capacity_switch;
    CB_FAILED;
}

switch(FEAT_TRAINS, SELF, maglev_flatbed_switch, current_callback) {
    VEH_CB_REFITTED_CAPACITY:  maglev_flatbed_wagon_cb_capacity_switch;
    VEH_CB_VEHICLE_PROPERTIES: maglev_flatbed_wagon_cb_property_switch;
    monorail_flatbed_graphics_switch;                                      // use monorail wagon graphics
}

item(FEAT_TRAINS, maglev_flatbed_wagon) {
    property {
        name:                           string(STR_NAME_FLATBED_WAGON);
        REFIT_PROPERTIES_FLATBED
        WAGON_PROPERTIES_DEFAULT
        introduction_date:              date(1925,1,1);
        model_life:                     VEHICLE_NEVER_EXPIRES;
        cost_factor:                    181;
        callback_flags:                 bitmask(VEH_CBF_REFITTED_CAPACITY);
        track_type:                     MGLV;
        cargo_capacity:                 37;
        weight:                         16;
    }
    graphics {
        maglev_flatbed_switch;
    }
}