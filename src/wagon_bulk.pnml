/* 
 * ***************************
 * Bulk wagon:
 *
 * coal, iron ore, bauxite, copper ore,
 * gravel, sand, clay, grains, maize, wheat,
 * sulphur, fertilizer, scrap metal,
 * fruit, fruit & vegetables, toffee,
 * sugar, cotton candy, sugar beet
 * lumber (wood chips)
 * ***************************
 */

spriteset(bulk_wagon_empty_set, "src/gfx/molace_wagons.png") { tmpl_railwagon(0,0) }

spriteset(bulk_wagon_coal_default_set, "src/gfx/molace_wagons.png") { tmpl_railwagon(127,0) }
spritegroup bulk_wagon_coal_default_group {
	loaded: [bulk_wagon_empty_set, bulk_wagon_coal_default_set];
	loading: [bulk_wagon_empty_set, bulk_wagon_coal_default_set];
}
spriteset(bulk_wagon_coal_arctic_empty_set, "src/gfx/arctic_railwagons.png") { tmpl_railwagon(0,25) }
spriteset(bulk_wagon_coal_arctic_full_set, "src/gfx/arctic_railwagons.png") { tmpl_railwagon(0,250) }
spritegroup bulk_wagon_coal_arctic_group {
	loaded: [bulk_wagon_coal_arctic_empty_set, bulk_wagon_coal_arctic_full_set];
	loading: [bulk_wagon_coal_arctic_empty_set, bulk_wagon_coal_arctic_full_set];
}
spriteset(bulk_wagon_coal2_empty_set, "src/gfx/molace_wagons.png") { tmpl_railwagon(0,842) }
spriteset(bulk_wagon_coal2_full_set, "src/gfx/molace_wagons.png") { tmpl_railwagon(127,842) }
spritegroup bulk_wagon_coal2_group {
	loaded: [bulk_wagon_coal2_empty_set, bulk_wagon_coal2_full_set];
	loading: [bulk_wagon_coal2_empty_set, bulk_wagon_coal2_full_set];
}


spriteset(bulk_wagon_gravel_set, "src/gfx/railwagon_gravel.png") { tmpl_railwagon(0,0) }
spritegroup bulk_wagon_gravel_group {
	loaded: [bulk_wagon_empty_set, bulk_wagon_gravel_set];
	loading: [bulk_wagon_empty_set, bulk_wagon_gravel_set];
}

spriteset(bulk_wagon_empty_grain_set, "src/gfx/temperate_railwagons.png") { tmpl_railwagon(0,150) }
spriteset(bulk_wagon_grain_set, "src/gfx/temperate_railwagons.png") { tmpl_railwagon(0,275) }
spritegroup bulk_wagon_grain_group {
	loaded: [bulk_wagon_empty_grain_set, bulk_wagon_grain_set];
	loading: [bulk_wagon_empty_grain_set, bulk_wagon_grain_set];
}

spriteset(bulk_wagon_empty_copperore_set, "src/gfx/tropic_railwagons.png") { tmpl_railwagon(0,225) }
spriteset(bulk_wagon_copperore_set, "src/gfx/tropic_railwagons.png") { tmpl_railwagon(0,450) }
spritegroup bulk_wagon_copperore_group {
	loaded: [bulk_wagon_empty_copperore_set, bulk_wagon_copperore_set];
	loading: [bulk_wagon_empty_copperore_set, bulk_wagon_copperore_set];
}

spriteset(bulk_wagon_bauxite_set, "src/gfx/railwagon_bauxite.png") { tmpl_railwagon(0,0) }
spritegroup bulk_wagon_bauxite_group {
	loaded: [bulk_wagon_empty_set, bulk_wagon_bauxite_set];
	loading: [bulk_wagon_empty_set, bulk_wagon_bauxite_set];
}

spriteset(bulk_wagon_empty_ironore_set, "src/gfx/temperate_railwagons.png") { tmpl_railwagon(0,225) }
spriteset(bulk_wagon_ironore_set, "src/gfx/temperate_railwagons.png") { tmpl_railwagon(0,350) }
spritegroup bulk_wagon_ironore_group {
	loaded: [bulk_wagon_empty_ironore_set, bulk_wagon_ironore_set];
	loading: [bulk_wagon_empty_ironore_set, bulk_wagon_ironore_set];
}

spriteset(bulk_wagon_sand_set, "src/gfx/railwagon_sand.png") { tmpl_railwagon(0,0) }
spritegroup bulk_wagon_sand_group {
	loaded: [bulk_wagon_empty_set, bulk_wagon_sand_set];
	loading: [bulk_wagon_empty_set, bulk_wagon_sand_set];
}

spriteset(bulk_wagon_empty_rawsugar_set, "src/gfx/tropic_railwagons.png") { tmpl_railwagon(0,500) }
spriteset(bulk_wagon_rawsugar_set, "src/gfx/tropic_railwagons.png") { tmpl_railwagon(0,525) }
spritegroup bulk_wagon_rawsugar_group {
	loaded: [bulk_wagon_empty_rawsugar_set, bulk_wagon_rawsugar_set];
	loading: [bulk_wagon_empty_rawsugar_set, bulk_wagon_rawsugar_set];
}

spriteset(bulk_wagon_cottoncandy_empty_set, "src/gfx/toyland_railwagons.png") { tmpl_short_railwagon(0,140) }
spriteset(bulk_wagon_cottoncandy_full_set, "src/gfx/toyland_railwagons.png") { tmpl_short_railwagon(0,160) }
spritegroup bulk_wagon_cottoncandy_group {
	loaded: [bulk_wagon_cottoncandy_empty_set, bulk_wagon_cottoncandy_full_set];
	loading: [bulk_wagon_cottoncandy_empty_set, bulk_wagon_cottoncandy_full_set];
}

spriteset(bulk_wagon_toffee_empty_set, "src/gfx/toyland_railwagons.png") { tmpl_short_railwagon(0,180) }
spriteset(bulk_wagon_toffee_full_set, "src/gfx/toyland_railwagons.png") { tmpl_short_railwagon(0,200) }
spritegroup bulk_wagon_toffee_group {
	loaded: [bulk_wagon_toffee_empty_set, bulk_wagon_toffee_full_set];
	loading: [bulk_wagon_toffee_empty_set, bulk_wagon_toffee_full_set];
}

spriteset(bulk_wagon_sugar_empty_set, "src/gfx/toyland_railwagons.png") { tmpl_short_railwagon(0,100) }
spriteset(bulk_wagon_sugar_full_set, "src/gfx/toyland_railwagons.png") { tmpl_short_railwagon(0,120) }
spritegroup bulk_wagon_sugar_group {
	loaded: [bulk_wagon_sugar_empty_set, bulk_wagon_sugar_full_set];
	loading: [bulk_wagon_sugar_empty_set, bulk_wagon_sugar_full_set];
}

spriteset(bulk_wagon_fruit_empty_set, "src/gfx/tropic_railwagons.png") { tmpl_railwagon(0,500) }
spriteset(bulk_wagon_fruit_full_set, "src/gfx/tropic_railwagons.png") { tmpl_railwagon(0,525) }
spritegroup bulk_wagon_fruit_group {
	loaded: [bulk_wagon_fruit_empty_set, bulk_wagon_fruit_full_set];
	loading: [bulk_wagon_fruit_empty_set, bulk_wagon_fruit_full_set];
}

random_switch (FEAT_TRAINS, SELF, bulk_wagon_coal_default_switch, TRIGGER_VEHICLE_SERVICE) {
    2: bulk_wagon_coal_default_group;
    1: bulk_wagon_coal2_group;
}

switch (FEAT_TRAINS, SELF, bulk_wagon_coal_climate_switch, climate) {
    CLIMATE_ARCTIC: bulk_wagon_coal_arctic_group;
    bulk_wagon_coal_default_switch;    
}

switch(FEAT_TRAINS, SELF, bulk_wagon_switch, cargo_type_in_veh) {
    AORE: bulk_wagon_bauxite_group;
    CLAY: bulk_wagon_gravel_group;
    COAL: bulk_wagon_coal_climate_switch;
    CORE: bulk_wagon_copperore_group;
    CTCD: bulk_wagon_cottoncandy_group;
    FERT: bulk_wagon_gravel_group;
    FRUT: bulk_wagon_fruit_group;
    FRVG: bulk_wagon_fruit_group;
    GRAI: bulk_wagon_grain_group;
    GRVL: bulk_wagon_gravel_group;
    IORE: bulk_wagon_ironore_group;
    MAIZ: bulk_wagon_grain_group;
    RSGR: bulk_wagon_rawsugar_group;
    SAND: bulk_wagon_sand_group;
    SUGR: bulk_wagon_sugar_group;
    SULP: bulk_wagon_sand_group;
    TOFF: bulk_wagon_toffee_group;
    WHEA: bulk_wagon_grain_group;
    bulk_wagon_coal_default_group;                // default to coal; also waste (WSTE)
}

switch (FEAT_TRAINS, SELF, bulk_wagon_cb_capacity_switch, cargo_type_in_veh) {
    // default: 30 units
    FICR: return 25;
    FRUT: return 25;
    FRVG: return 25;
    RSGR: return 20;
    RUBR: return 21;
    SWET: return 25;
    CB_FAILED;
}

switch (FEAT_TRAINS, SELF, bulk_wagon_cb_property_weight_switch, cargo_type_in_veh) {
    // default: 19 tons
    COAL: return 18;
    FRUT: return 18;
    FRVG: return 18;
    PLAS: return 18;
    RSGR: return 18;
    SWET: return 21;
    TOFF: return 20;
    CB_FAILED;
}

switch (FEAT_TRAINS, SELF, bulk_wagon_cb_property_switch, extra_callback_info1) {
    0x16: bulk_wagon_cb_property_weight_switch;
    CB_FAILED;
}

switch (FEAT_TRAINS, SELF, bulk_wagon_cb_switch, current_callback) {
    VEH_CB_REFITTED_CAPACITY:  bulk_wagon_cb_capacity_switch;
    VEH_CB_VEHICLE_PROPERTIES: bulk_wagon_cb_property_switch;
    bulk_wagon_switch;
}


item(FEAT_TRAINS, bulk_wagon) {
    property {
        // We try to simulate the stats of the temperate grain wagon
        name:                           string(STR_NAME_BULK_WAGON);
        climates_available:             ALL_CLIMATES;
        refittable_cargo_classes:       bitmask(CC_BULK);
        non_refittable_cargo_classes:   bitmask(CC_PASSENGERS, CC_MAIL, CC_ARMOURED, CC_LIQUID, CC_REFRIGERATED, CC_HAZARDOUS);
        refittable_cargo_types:         bitmask(SCRP, FRUT, FRVG);
        sprite_id:                      SPRITE_ID_NEW_TRAIN;
        introduction_date:              date(1925,1,1);
        model_life:                     50;
        retire_early:                   0;
        vehicle_life:                   20;
        reliability_decay:              20;
        loading_speed:                  5;
        cost_factor:                    182;
        running_cost_factor:            5;
        speed:                          0;
        refit_cost:                     40;
        callback_flags:                 VEH_CBF_REFITTED_CAPACITY;
        track_type:                     RAIL;
        power:                          0;
        running_cost_base:              RUNNING_COST_STEAM;
        cargo_capacity:                 30;
        weight:                         19 ton;
        bitmask_vehicle_info:           0;
    }
    graphics {
        bulk_wagon_cb_switch;
    }
}