/* 
 * ***************************
 * Piece goods wagon:
 *
 * goods, food, paper, fruits, fruit + vegetables,
 * farm supplies, engineering supplies, 
 * manufacturing supplies, wool
 * ***************************
 */

switch (FEAT_TRAINS, SELF, maglev_piecegoods_wagon_cb_capacity_switch, cargo_type_in_veh) {
    // default: 32 units
    FOOD: return 27;
    FRUT: return 27;
    FRVG: return 27;
    PAPR: return 27;
    TOYS: return 27;
    CB_FAILED;
}

switch (FEAT_TRAINS, SELF, maglev_piecegoods_wagon_cb_property_switch, extra_callback_info1) {
    0x16: piecegoods_wagon_cb_property_weight_switch;                       // rail wagon weight is the same, use it.
    0x14: maglev_piecegoods_wagon_cb_capacity_switch;
    CB_FAILED;
}

switch (FEAT_TRAINS, SELF, maglev_piecegoods_switch, current_callback) {
    VEH_CB_REFITTED_CAPACITY:  maglev_piecegoods_wagon_cb_capacity_switch;
    VEH_CB_VEHICLE_PROPERTIES: maglev_piecegoods_wagon_cb_property_switch;
    monorail_piecegoods_cargo_switch;                                       // re-use graphics from monorail
}

item(FEAT_TRAINS, maglev_piecegoods_wagon) {
    property {
        name:                           string(STR_NAME_COVERED_PIECE_GOODS_WAGON);
        REFIT_PROPERTIES_PIECE_GOODS
        WAGON_PROPERTIES_DEFAULT
        introduction_date:              date(1925,1,1);
        model_life:                     VEHICLE_NEVER_EXPIRES;
        cost_factor:                    190;
        callback_flags:                 bitmask(VEH_CBF_REFITTED_CAPACITY);
        track_type:                     MGLV;
        cargo_capacity:                 32;
        weight:                         21;
    }
    graphics {
        maglev_piecegoods_switch;
    }
}