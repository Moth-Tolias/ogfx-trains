/* 
 * ***************************
 * Piece goods wagon:
 *
 * goods, food, paper, fruits, fruit + vegetables,
 * farm supplies, engineering supplies, 
 * manufacturing supplies, wool
 * ***************************
 */

spriteblock(FEAT_TRAINS) {
    spriteset(monorail_piecegoods_default_open_set, "src/gfx/temperate_monolev.png") { tmpl_short_railwagon(0,200) }
    spriteset(monorail_piecegoods_default_closed_set, "src/gfx/temperate_monolev.png") { tmpl_short_railwagon(0,220) }
    spritegroup monorail_piecegoods_default_group {
        loaded: monorail_piecegoods_default_closed_set;
        loading: monorail_piecegoods_default_open_set;
    }
	
    spriteset(monorail_piecegoods_goods_arctic_open_set, "src/gfx/arctic_monolev.png") { tmpl_short_railwagon(0,200) }
    spriteset(monorail_piecegoods_goods_arctic_closed_set, "src/gfx/arctic_monolev.png") { tmpl_short_railwagon(0,220) }
    spritegroup monorail_piecegoods_goods_arctic_group {
        loaded: monorail_piecegoods_goods_arctic_closed_set;
        loading: monorail_piecegoods_goods_arctic_open_set;
    }
    spriteset(monorail_piecegoods_goods_tropic_open_set, "src/gfx/tropic_monolev.png") { tmpl_short_railwagon(0,120) }
    spriteset(monorail_piecegoods_goods_tropic_closed_set, "src/gfx/tropic_monolev.png") { tmpl_short_railwagon(0,140) }
    spritegroup monorail_piecegoods_goods_tropic_group {
        loaded: monorail_piecegoods_goods_tropic_closed_set;
        loading: monorail_piecegoods_goods_tropic_open_set;
    }

    spriteset(monorail_piecegoods_paper_open_set, "src/gfx/arctic_monolev.png") { tmpl_short_railwagon(0,360) }
    spriteset(monorail_piecegoods_paper_closed_set, "src/gfx/arctic_monolev.png") { tmpl_short_railwagon(0,380) }
    spritegroup monorail_piecegoods_paper_group {
        loaded: monorail_piecegoods_paper_closed_set;
        loading: monorail_piecegoods_paper_open_set;
    }

    spriteset(monorail_piecegoods_food_open_set, "src/gfx/tropic_monolev.png") { tmpl_short_railwagon(0,240) }
    spriteset(monorail_piecegoods_food_closed_set, "src/gfx/tropic_monolev.png") { tmpl_short_railwagon(0,260) }
    spritegroup monorail_piecegoods_food_group {
        loaded: monorail_piecegoods_food_closed_set;
        loading: monorail_piecegoods_food_open_set;
    }
}

switch(FEAT_TRAINS, SELF, monorail_piecegoods_goods_climate_switch, climate) {
	CLIMATE_ARCTIC: monorail_piecegoods_goods_arctic_group;
	CLIMATE_TROPIC: monorail_piecegoods_goods_tropic_group;
	monorail_piecegoods_default_group;
}

switch(FEAT_TRAINS, SELF, monorail_piecegoods_cargo_switch, cargo_type_in_veh) {
	FOOD: monorail_piecegoods_food_group;
	GOOD: monorail_piecegoods_goods_climate_switch;
	PAPR: monorail_piecegoods_paper_group;
	monorail_piecegoods_default_group;
}

switch (FEAT_TRAINS, SELF, monorail_piecegoods_switch, current_callback) {
    VEH_CB_REFITTED_CAPACITY:  piecegoods_wagon_cb_capacity_switch; // use rail wagon properties
    VEH_CB_VEHICLE_PROPERTIES: piecegoods_wagon_cb_property_switch; // use rail wagon properties
    monorail_piecegoods_cargo_switch;
}

item(FEAT_TRAINS, monorail_piecegoods_wagon) {
    property {                                                // we re-use the default name + properties
        climates_available:             CLIMATE_ALL;
        refittable_cargo_classes:       CC_PIECE_GOODS | CC_EXPRESS;
        non_refittable_cargo_classes:   CC_PASSENGERS | CC_MAIL | CC_BULK | CC_ARMOURED | CC_LIQUID | CC_OVERSIZED | CC_REFRIGERATED;
        refittable_cargo_types:         bitmask(WOOD, LVST, STEL, SCRP, VEHI, BRCK, BUBL, FOOD, FRUT, FRVG);
        sprite_id:                      SPRITE_ID_NEW_TRAIN;
        introduction_date:              date(1980,1,1);
        model_life:                     VEHICLE_NEVER_EXPIRES;
        retire_early:                   0;
        vehicle_life:                   30;
        reliability_decay:              0;
        loading_speed:                  10;
        cost_factor:                    190;
        running_cost_factor:            5;
        speed:                          0;
        refit_cost:                     40;
        callback_flags:                 VEH_CBF_REFITTED_CAPACITY;
        track_type:                     MONO;
        power:                          0;
        running_cost_base:              RUNNING_COST_STEAM;
        cargo_capacity:                 25;
        weight:                         21;
        bitmask_vehicle_info:           0;
    }
    graphics {
        monorail_piecegoods_switch;
    }
}